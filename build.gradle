buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }

}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = 'Finchley.RELEASE'

    ext {
        drivers = [
                'chromeHeadless'
        ]
    }
}

def today = java.time.Instant.now()
group = 'uk.gov.justice.digital.hmpps'
project.sourceCompatibility = 10
project.targetCompatibility = 10

def todayDate = java.time.LocalDate.now()
def formatter = java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd")
version todayDate.format(formatter)


springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital.hmpps'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
                ]
        }
    }
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor('org.projectlombok:lombok:1.18.2')

    compile files('lib/ojdbc8-12.2.0.1.jar')

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.0.9.RELEASE'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.3.3.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-actuator'

    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.hibernate:hibernate-java8")

    compile("net.logstash.logback:logstash-logback-encoder:5.2")
    compile("com.microsoft.azure:applicationinsights-core:2.1.2")
    compile("com.microsoft.azure:applicationinsights-web:2.1.2")
    compile("com.microsoft.azure:applicationinsights-logging-logback:2.1.2")

    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'com.sun.xml.bind:jaxb-impl:2.3.0'
    compile 'com.sun.xml.bind:jaxb-core:2.3.0'
    compile 'javax.activation:activation:1.1.1'

    compile('javax.transaction:javax.transaction-api:1.3')

    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'

    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'
    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.3.0'
    compile 'org.webjars:bootstrap:3.3.7'

    compile 'org.flywaydb:flyway-core:5.2.0'
    compile 'com.zaxxer:HikariCP:2.7.6'
    compile("org.apache.commons:commons-lang3")
    runtime 'com.h2database:h2:1.4.196'
    runtime('org.springframework.boot:spring-boot-devtools')

    compileOnly('org.projectlombok:lombok:1.18.2')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testRuntime 'com.h2database:h2:1.4.196'

    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.14'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: "org.codehaus.groovy"
    }

    testCompile 'org.gebish:geb-core:2.2'
    testCompile 'org.gebish:geb-spock:2.2'
    testCompile 'org.seleniumhq.selenium:selenium-support:3.12.0'
    testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.12.0'
    testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:3.12.0'
    testCompile 'io.github.http-builder-ng:http-builder-ng-apache:1.0.3'

    testCompile 'com.github.tomakehurst:wiremock-standalone:2.15.0'
    testCompile 'com.github.tomjankes:wiremock-groovy:0.2.0'
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'com.auth0:java-jwt:3.3.0'
    
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false } // Always run tests

        systemProperty 'geb.build.reportsDir', reporting.file("geb/${name}")
        systemProperty 'geb.env', driver
    }
}


test {
    exclude '**/integration/*'
}

task testIntegration(type: Test) {
    include 'uk/gov/justice/digital/hmpps/oauth2server/integration/specs/*'
}

