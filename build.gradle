buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: "com.github.ben-manes.versions"

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/gov-uk-notify/maven" }
    maven { url "https://raw.github.com/weddini/spring-boot-throttling/mvn-repo/" }
}

ext {
    springCloudVersion = 'Finchley.RELEASE'

    ext {
        drivers = [
                'chromeHeadless'
        ]
    }
}

def today = java.time.Instant.now()
group = 'uk.gov.justice.digital.hmpps'
project.sourceCompatibility = 11
project.targetCompatibility = 11

def todayDate = java.time.LocalDate.now()
def formatter = java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd")
version todayDate.format(formatter)


springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital.hmpps'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor('org.projectlombok:lombok:1.18.6')

    compile files('lib/ojdbc8-18.3.jar')

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.security:spring-security-jwt:1.0.10.RELEASE'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.3.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'io.jsonwebtoken:jjwt:0.9.1'
    compile 'com.weddini.throttling:spring-boot-throttling-starter:0.0.9'

    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.hibernate:hibernate-java8'

    compile 'net.logstash.logback:logstash-logback-encoder:5.3'
    compile 'com.microsoft.azure:applicationinsights-spring-boot-starter:1.1.1'
    compile 'com.microsoft.azure:applicationinsights-logging-logback:2.3.1'

    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'javax.xml.bind:jaxb-api:2.3.1'
    compile 'com.sun.xml.bind:jaxb-impl:2.3.2'
    compile 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    compile 'javax.activation:activation:1.1.1'

    compile 'javax.transaction:javax.transaction-api:1.3'

    compile 'io.springfox:springfox-swagger2:2.9.2'
    compile 'io.springfox:springfox-swagger-ui:2.9.2'

    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.3.0'
    compile 'org.webjars:bootstrap:4.1.3'
    compile 'uk.gov.service.notify:notifications-java-client:3.14.1-RELEASE'

    compile 'org.flywaydb:flyway-core:5.2.4'
    compile 'com.zaxxer:HikariCP:3.3.1'
    compile 'org.apache.commons:commons-lang3'
    compile 'org.apache.commons:commons-text:1.6'
    runtime 'com.h2database:h2:1.4.199'
    compile 'com.microsoft.sqlserver:mssql-jdbc:7.2.2.jre11'
    compile 'com.github.timpeeters:spring-boot-graceful-shutdown:2.1.1'
    runtime 'org.springframework.boot:spring-boot-devtools'

    compileOnly 'org.projectlombok:lombok:1.18.6'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testRuntime 'com.h2database:h2:1.4.196'

    testCompile 'org.codehaus.groovy:groovy-all:2.5.4'
    testCompile 'org.spockframework:spock-spring:1.2-groovy-2.5'
    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }

    testCompile 'org.gebish:geb-core:2.3.1'
    testCompile 'org.gebish:geb-spock:2.3.1'
    testCompile 'org.seleniumhq.selenium:selenium-support:3.141.59'
    testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
    testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:3.141.59'
    testCompile 'io.github.http-builder-ng:http-builder-ng-apache:1.0.3'

    testCompile 'com.github.tomakehurst:wiremock-standalone:2.23.2'
    testCompile 'com.github.tomjankes:wiremock-groovy:0.2.0'
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'com.auth0:java-jwt:3.8.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false } // Always run tests

        systemProperty 'geb.build.reportsDir', reporting.file("geb/${name}")
        systemProperty 'geb.env', driver
    }
}


test {
    exclude '**/integration/*'
}

task testIntegration(type: Test) {
    include 'uk/gov/justice/digital/hmpps/oauth2server/integration/specs/*'
}

